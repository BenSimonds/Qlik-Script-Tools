///$tab Main
SET ThousandSep=',';
SET DecimalSep='.';
SET MoneyThousandSep=',';
SET MoneyDecimalSep='.';
SET MoneyFormat='£#,##0.00;-£#,##0.00';
SET TimeFormat='hh:mm:ss';
SET DateFormat='DD/MM/YYYY';
SET TimestampFormat='DD/MM/YYYY hh:mm:ss[.fff]';
SET MonthNames='Jan;Feb;Mar;Apr;May;Jun;Jul;Aug;Sep;Oct;Nov;Dec';
SET DayNames='Mon;Tue;Wed;Thu;Fri;Sat;Sun';


///$tab SmartCall_Init


//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Generic set up
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

SET HidePrefix = '_';
LET vDocumentName = Documentname();
LET vCounter = 0; // Each tab will increment this variable

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Variables for calculation of the total elapsed time (see before the EXIT)
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

LET vScriptStartTimestampNumeric = Num(Now());
LET vScriptStartTimestamp = Timestamp($(vScriptStartTimestampNumeric));

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Initialization of the Metadata table. Used for concatenation.
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Tables_Metadata:
LOAD
'' AS TM_TableName
AutoGenerate 0;

///$tab Sub_Metadata
// Tab Start

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// SUB TablesMetadata
// It runs at the end of each extraction, capturing the metadata info and adding it to one record of the metadata table
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


SUB TablesMetadata

LET vExtractCurrentEnd = num(Now()); 													// The vExtractCurrentStart must be declared at the beginning of the new tab
LET vExtractCurrentElapsedTime = ($(vExtractCurrentEnd)-$(vExtractCurrentStart))*86400; // Elapsed time in seconds
LET vNoOfRows = NoOfRows('$(vTable)'); 													// Based on the table in memory. In general it can be smaller than the one in the DB
LET vNoOfFields = NoOfFields('$(vTable)'); 												// Based on the table in memory. In general it can be smaller than the one in the DB

Concatenate (Tables_Metadata) // Requires the existance of a table called Tables_Metadata
LOAD
'$(vCurrentSub)' 								AS _KEY_SUB,		// Used as a key, not visible in front end
$(vCounter)										AS TM_SubID,
'$(vCurrentSub)'								AS TM_SubName,
'$(vCounter)'&' '&'$(vCurrentSub)'				AS TM_Sub,
'$(vTable)' 									AS TM_TableName, 	// Table Name
Date($(vExtractCurrentStart))					AS TM_Date,			//May want to have start and end date for very long extracts...
Time($(vExtractCurrentStart)) 					AS TM_StartTime,
Time($(vExtractCurrentEnd)) 					AS TM_EndTime,
Time($(vExtractCurrentElapsedTime)/86400 ) 		AS TM_ElapsedTime, 
Num('$(vExtractCurrentElapsedTime)')			AS TM_ElapsedSeconds, // Easy to sum in the chart but not easy to read when the number is big
Num('$(vNoOfRows)') 							AS TM_NoOfRows,
Num('$(vNoOfFields)') 							AS TM_NoOfColumns,
'ALL_TABLE_METADATA'							AS ALL_TABLE_METADATA
AutoGenerate 1;

END SUB

// Tab End
///$tab Template_Start
SUB TEST

LET vTable = 'TEST';
LET vCounter = $(vCounter)+1;
TRACE $(vCounter) - Running tab $(vTable) of document $(vDocumentName) with vSmartVar $(vSmartVarPassedFromExternalCall);
LET vExtractCurrentStart = num(Now());

/*
Notes:
*/
///$tab Template_Mid
TEST:
Load
	'Test Ran!' as TEST_Field
AutoGenerate(1);

SLEEP 1000;
TRACE >> Test Ran!;

///$tab Template_End
CALL TablesMetadata
END SUB
///$tab SmartCall_Run
// Tab Start    

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// This Tab is the place where everything happens
// The INLINE table called SubNameList contains the list of the existing SUBROUTINES, and the associated table
// The loop is running as many times as many records has this table
// Each round of the loop is comparing the current SUBROUTINE XXX with the string.
// If _XXX_ is contained in the string, then the SUBROUTINE XXX will be called, otherwise nothing will happen
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

//SET vSmartVar = $(vSmartVarPassedFromExternalCall); // The string must be passed from the external call. Alternatively, it can be manually inserted in the Input Field object
If '$(vSmartVarPassedFromExternalCall)' = ' ' THEN
	SET vSmartVar = _TEST_; //Set this to whatever you want the default run to be.
ELSE 
	SET vSmartVar = $(vSmartVarPassedFromExternalCall);
ENDIF

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// List of SUBs. In general it should be all uncommented: if they are not in the vSmartVar, they will be ignored in any case
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

SubNameList:
LOAD 
SubName AS _KEY_SUB,
Rowno() AS _SubNameListSort,
* 
INLINE [
SubName, SubNote
Test, SubName is not case sensitive.
]
WHERE LEFT(SubName,2)<>'//'
;

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Loop initialization
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


LET vNumberOfRowsOfINLINE = NoOfRows('SubNameList');
LET vNumberOfLoops=$(vNumberOfRowsOfINLINE);

FOR vCurrentLoop = 1 to $(vNumberOfLoops)
LET vCurrentRecord = $(vCurrentLoop)-1;  //The first record of a table by convention is 0, not 1
LET vCurrentSub = Peek('SubName',$(vCurrentRecord),'SubNameList');  //Takes one by one the records of the column "SubName" of the table "SubNameList"
TRACE Execution of loop $(vCurrentLoop) of $(vNumberOfLoops)  Launching Sub $(vCurrentSub); //Gives in the dialogue box a feedback of what's happening

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ------------------------------------> LOOP <-----------------------------------
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

IF SubStringCount(UPPER('$(vSmartVar)'),'_'&UPPER('$(vCurrentSub)')&'_') THEN // NOTE: the separator is needed, because otherwise the string FTM will also execute the FT 
Call $(vCurrentSub);
ELSE 
TRACE The SUB called "$(vCurrentSub)" is not in the variable, therefore it will not be executed;
END IF

NEXT vCurrentLoop;


//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Cleanup of variables that are not needed in the front end
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

LET vCurrentLoop = ;
LET vNumberOfRowsOfINLINE = ;
LET vNumberOfLoops = ;
LET vCurrentRecord = ;
LET vCurrentSub = ;

// Tab End

///$tab EXIT
exit script;
